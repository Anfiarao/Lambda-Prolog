%%-- calc_singleton.elpi

%ofType star singleton set IE.

type  singleton mttType.
type  star mttTerm.
type  elim_singleton mttTerm -> (mttTerm -> mttType) -> mttTerm.

isType singleton set IE.

of star singleton IE.

of (elim_singleton Star C Cc) (C Star) IE :-
    of Star singleton IE,
    of Cc (C star) IE,
    (pi z\ locDecl z singleton => isType (C z) col IE).

hstep (elim_singleton star C Cc) Cc :-
    of Cc (C star) IE,
    (pi z\ locDecl z singleton => isType (C z) col IE ).

dstep (elim_singleton T C CC) (elim_singleton T' C CC) :- dstep T T'.
dstep (elim_singleton T C CC) (elim_singleton T C CC') :- dstep CC CC'.
dstep (elim_singleton T C CC) (elim_singleton T C' CC) :-
    (pi z\ locDecl z singleton => dstep (C z) (C' z)).






