
isType prop (propId A AA1 AA2) :- 
    isa int AA1 A,
    isa itn AA2 A,
    isType ANY A.

of int (id A AA) (propId A AA AA) :-
    isa int AA A,
    isType ANY A.  

of int (elim_id P C CC) (C AA1 AA2) :-
    of int P (propId A AA1 AA2),
    (pi x\ pi y\ of int x A => of int y A => isType prop (C x y) ),
    (pi x\ of int x A => of int (CC x) (C x x)),
    of int AA1 A,
    of int AA2 A.

hstep (elim_id (id A AA) C CC) (CC AA) :-
    of int AA A,
    (pi x\ pi y\ of int x A => of int y A => isType prop (C x y) ),
    (pi x\ of int x A => of int (CC x) (C x x)).

dstep (id A AA) (id A AA') :- dstep AA AA'.

dstep (id A AA) (id A' AA) :- dstep A A'.

dstep (propId A Aa Ab) (propId A' Aa Ab) :- dstep A A'.
dstep (propId A Aa Ab) (propId A Aa' Ab) :- dstep Aa Aa'.
dstep (propId A Aa Ab) (propId A Aa Ab') :- dstep Ab Ab'.

dstep (elim_id P C CC) (elim_id P' C CC)  :- dstep P P'.
dstep (elim_id P C CC) (elim_id P C' CC)  :- 
    of int P (propId A _ _),
    pi x\ pi y\ of int x A => of int y A => dstep (C x y) (C' x y).

dstep (elim_id P C CC) (elim_id P C CC')  :- 
    of int P (propId A _ _),
    pi x\ of int x A => dstep (CC x) (CC' x).

